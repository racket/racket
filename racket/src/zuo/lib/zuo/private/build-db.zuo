#lang zuo/base

;; Build results for each target are stored in the target's directory
;; in "_zuo.db". Since multiple targets are likely to be in the same
;; directory, the goal here is to be able to load information for all
;; the targets at once.

;; A timestamp-based SHA-1 cache for input files is stored in
;; "_zuo_tc.db" alongside "_zuo.db" --- in the directory of a target
;; that depends on the input files, not in the input file's directory
;; (which is likely to be in the source tree). An input used by
;; targets in different directories will have information cached in
;; each of those directories. The cache may also include information
;; for non-input targets that are dependencies, just because it's
;; easier to not distinguish when writing.

;; Paths are stored in the ".db" files as absolute (when they started
;; that way in a target) or relative to the file's directory. The
;; format of each file is a top-level sequence of
;;   (<rel-path> . <info>)
;; For "_zuo.db", it's more specifically
;;   (<rel-path> <sha1> (<dep-rel-path> <sha1>) ...)
;; For "_zuo_tc.db", it's
;;   (<rel-path> (<time-secs> . <time-msec>) <sha1>)

(provide db-record-target-sha1s
         db-load-sha1s

         file-sha1/cached
         path->absolute-path
         dir-part

         symbol->key
         symbol-key?
         symbol-key->symbol)

(define (db-file-path target-dir) (build-path target-dir "_zuo.db"))
(define (tc-file-path target-dir) (build-path target-dir "_zuo_tc.db"))

;; for serialization and deserialization of dep-sha1s tables
(define (hash->list ht) (map (lambda (k) (list (serialize-key k) (hash-ref ht k))) (hash-keys ht)))
(define (list->hash l) (foldl (lambda (p ht) (hash-set ht (deserialize-key (car p)) (cadr p)))
                              (hash)
                              l))
(define (serialize-key key) (if (symbol-key? key)
                                (symbol-key->symbol key)
                                (symbol->string key))) ; represents a path
(define (deserialize-key key) (if (string? key)
                                  (string->symbol key)  ; represents a path
                                  (symbol->key key)))

;; Records the result of a build of `abs-path`, mainly storing the
;; SHA-1 and dep SHA-1s in "_zuo.db", but also recording a timestamp
;; plus SHA-1 for dependencies in "_zuo_tc.db"
(define (db-record-target-sha1s target-dir+name ts)
  (define dir (or (car target-dir+name) "."))
  (define name (cdr target-dir+name))
  (define db-path (db-file-path dir))
  (define tc-path (tc-file-path dir))
  (define db-content
    (if (file-exists? db-path)
        (string-read (file->string db-path) 0 db-path)
        '()))
  (define tc-content
    (if (file-exists? tc-path)
        (string-read (file->string tc-path) 0 tc-path)
        '()))
  (define dep-sha1s-l (hash->list (cdr ts)))
  (define new-db-content (reassoc (list* name (car ts) dep-sha1s-l) db-content))
  (define new-tc-content
    (foldl (lambda (dep tc-content)
             (define dep-name (car dep))
             (cond
               [(symbol? dep-name) tc-content]
               [else
                (define time (file-timestamp (if (relative-path? dep-name)
                                                 (build-path dir dep-name)
                                                 dep-name)))
                (cond
                  [time (reassoc (list dep-name time (cadr dep)) tc-content)]
                  [else tc-content])]))
           tc-content
           (cons (list name (car ts)) dep-sha1s-l)))
  (update-file db-path new-db-content)
  (update-file tc-path new-tc-content))

;; Loads previous-build information for `abs-path`, as well as cached
;; SHA-1s for things that might be dependencies; loading needs to
;; happen only once per directory that has a (non-input) build target
(define (db-load-sha1s target-dir+name db tc)
  (define dir (or (car target-dir+name) "."))
  (define name (cdr target-dir+name))
  (define db-path (db-file-path dir))
  (define tc-path (tc-file-path dir))
  (define key (string->symbol db-path))
  (cond
    [(hash-ref db key #f) #f]
    [else
     ;; if loading fails, then we'll delete files on the
     ;; grounds that they must be in bad shape
     (define (read-in path table deserialize)
       (suspend-signal) ; don't lose the file as a result of Ctl-C
       (define c-handle (cleanable-file path))
       (define content (if (file-exists? path)
                           (string-read (file->string path) 0 path)
                           '()))
       (define new
         (foldl (lambda (name+val table)
                  (define name (car name+val))
                  (define key (string->symbol (if (relative-path? name)
                                                  (build-path dir name)
                                                  name)))
                  (hash-set table key (deserialize (cdr name+val))))
                table
                content))
       (cleanable-cancel c-handle)
       (resume-signal)
       new)
     (define new-db (read-in db-path (hash-set db key #t) (lambda (v)
                                                            (cons (car v)
                                                                  (list->hash (cdr v))))))
     (define new-tc (read-in tc-path tc (lambda (v) v)))
     (cons new-db new-tc)]))

;; Helpers to get an input file's SHA-1, possibly cached
(define (file-sha1/cached path time-cache)
  (let ([timestamp (file-timestamp path)])
    (and timestamp
         (let ([cached (and time-cache
                            (hash-ref time-cache
                                      (string->symbol path)
                                      #f))])
           (if (and cached
                    (equal? (car cached) timestamp))
               (cadr cached)
               (string-sha1 (file->string path)))))))

;; Atomic write by write-to-temporary-and-move
(define (update-file path new-content)
  (define path-tmp (~a path "-tmp"))
  (display-to-file (string-join (map ~s new-content) "\n") path-tmp :truncate)
  (when (eq? 'windows (hash-ref (runtime-env) 'system-type))
    (when (file-exists? path) (rm path)))
  (mv path-tmp path))

;; Like `hash-set`, but for an association list
(define (reassoc pr content)
  (cond
    [(null? content) (list pr)]
    [(string=? (caar content) (car pr)) (cons pr (cdr content))]
    [else (cons (car content) (reassoc pr (cdr content)))]))

(define (file-timestamp path)
  (define s (stat path))
  (and s (list (hash-ref s 'modify-time-seconds)
               (hash-ref s 'modify-time-nanoseconds))))

(define (path->absolute-path p)
  (if (relative-path? p)
      (build-path (hash-ref (runtime-env) 'dir) p)
      p))

(define (dir-part path)
  (if (symbol? path)
      "."
      (or (car (split-path path)) ".")))

(define (symbol->key name)
  (string->uninterned-symbol (~a "!" (symbol->string name))))

(define (symbol-key? sym)
  (let ([str (symbol->string sym)])
    (and (= (char "!") (string-ref str 0))
         (not (eq? sym (string->symbol str))))))

(define (symbol-key->symbol sym)
  (let ([str (symbol->string sym)])
    (string->symbol (substring str 1 (string-length str)))))



         
